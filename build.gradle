//file:noinspection GradlePackageVersionRange
plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_base_version}"
group = project.maven_group

def ENV = System.getenv()

repositories {
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://maven.wispforest.io' }
    maven { url "https://maven.shedaniel.me/" }
    maven {
        url 'https://api.modrinth.com/maven'
        content { includeGroup "maven.modrinth" }
    }
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

    modCompileOnly ("me.shedaniel:RoughlyEnoughItems-api-fabric:$rei_version")
    modCompileOnly ("me.shedaniel:RoughlyEnoughItems-default-plugin:$rei_version")
    modCompileOnly ("me.shedaniel.cloth:basic-math:0.6.1")

    modLocalRuntime ("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}")

//    modCompileOnly ("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}") {
//        exclude(module: "cloth-config-fabric")
//    }
//    modCompileOnly ("me.shedaniel.cloth:basic-math:0.6.1")
//    modCompileOnly ("dev.architectury:architectury-fabric:11.0.9")
//
//    modLocalRuntime ("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
//        exclude(module: "cloth-config-fabric")
//        exclude(module: "architectury-fabric")
//    }
//    modLocalRuntime ("me.shedaniel.cloth:cloth-config-fabric:13.0.121") {
//        exclude(module: "fabric-api")
//        exclude(module: "architectury-fabric")
//    }

    //modLocalRuntime ("dev.architectury:architectury-fabric:11.0.9") { exclude(module: "fabric-api") }

    //modLocalRuntime "dev.architectury:architectury-fabric:${project.arch_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    include "io.wispforest:owo-sentinel:${project.owo_version}"

    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    //modLocalRuntime ("maven.modrinth:spark:1.10.58-fabric")

    modLocalRuntime ("net.fabricmc:fabric-language-kotlin:1.10.17+kotlin.1.9.22")
    modLocalRuntime ("maven.modrinth:architectury-api:11.0.9+fabric")

    modLocalRuntime ("maven.modrinth:observable:4.4.0+fabric")
}

loom {
    accessWidenerPath = file("src/main/resources/alloy_forgery.accesswidener")

    runs {

        datagenClient {
            inherit client
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=alloy_forgery"

            runDir "build/datagen"
        }

        client_mixin {
            client()
            ideConfigGenerated true
            name = "[${project.archivesBaseName}] Client - (Mixin Swap)"
            source sourceSets.main
            vmArg "-Dmixin.debug.export=true"

            try {
                afterEvaluate {
                    def mixinDep = this.configurations.compileClasspath
                            .allDependencies
                            .findAll { it.name == "sponge-mixin" }
                            .first()

                    if (mixin != null) {
                        def mixinPath = this.configurations.compileClasspath.files(mixinDep).first().path;

                        println(mixinPath)

                        vmArg("-javaagent:\"${mixinPath}\"")

                        println("[Info]: Mixin Hotswap Run should be working")
                    } else {
                        println("[Warning]: Unable to locate file path for Mixin Jar, HotSwap Run will not work!!!")
                    }
                }
            } catch (Exception e) {
                println("[Error]: MixinHotswap Run had a issue!")
                e.printStackTrace()
            }
        }

        seperateFolderServer {
            inherit server
            name "Minecraft Server: Other"
            runDir "run_server"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}
